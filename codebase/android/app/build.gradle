plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "com.google.gms.google-services"
    // id "com.google.devtools.ksp" version "1.6.10-1.0.4"
}

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.makeappssimple.abhimanyu.financemanager.android"
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
        versionCode rootProject.appVersionCode
        versionName rootProject.appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        // Room schema
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        // should correspond to key/value pairs inside the file
        buildConfigField("String", "OPEN_EMOJI_KEY", apikeyProperties["OPEN_EMOJI_KEY"])
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-Xopt-in=kotlin.time.ExperimentalTime"
        freeCompilerArgs += "-Xopt-in=kotlin.contracts.ExperimentalContracts"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ObsoleteCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.InternalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
        freeCompilerArgs += "-Xopt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        freeCompilerArgs += "-Xopt-in=androidx.compose.animation.core.InternalAnimationApi"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion jetpackComposeVersion
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    // Room schema for testing
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {

    // Androidx core
    implementation "androidx.core:core-ktx:$rootProject.androidXCoreVersion"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$rootProject.lifecycleViewModelVersion"

    // Appcompat
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"

    // Jetpack compose
    implementation "androidx.compose.ui:ui:$rootProject.jetpackComposeVersion"

    // Jetpack compose tooling support (Previews, etc.)
    debugImplementation "androidx.compose.ui:ui-tooling:$rootProject.jetpackComposeVersion"
    implementation "androidx.compose.ui:ui-tooling-preview:$rootProject.jetpackComposeVersion"

    // Icons
    implementation "androidx.compose.material:material-icons-extended:$rootProject.jetpackComposeVersion"

    // Jetpack compose material design
    implementation "androidx.compose.material:material:$rootProject.jetpackComposeVersion"
    implementation "androidx.compose.material3:material3:$rootProject.jetpackComposeMaterial3Version"

    // Jetpack compose material design adapter
    implementation "com.google.android.material:compose-theme-adapter:$rootProject.jetpackComposeMaterialDesignAdapterVersion"

    // Jetpack compose integration with activities
    implementation "androidx.activity:activity-compose:$rootProject.jetpackComposeActivityIntegrationVersion"

    // Jetpack compose integration with view models
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$rootProject.jetpackComposeViewmodelIntegrationVersion"

    // Jetpack compose navigation
    implementation "androidx.navigation:navigation-compose:$rootProject.jetpackComposeNavigationVersion"

    // Jetpack compose navigation with hilt
    implementation "androidx.hilt:hilt-navigation-compose:$rootProject.jetpackComposeNavigationHiltVersion"

    // Accompanist
    implementation "com.google.accompanist:accompanist-insets:$rootProject.accompanistVersion"

    // Accompanist flow layout
    implementation "com.google.accompanist:accompanist-flowlayout:$rootProject.accompanistVersion"

    // Accompanist pager
    implementation "com.google.accompanist:accompanist-pager:$rootProject.accompanistVersion"

    // Accompanist pager indicators
    implementation "com.google.accompanist:accompanist-pager-indicators:$rootProject.accompanistVersion"

    // Hilt
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$rootProject.hiltVersion"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$rootProject.hiltLifecycleViewmodelVersion"
    kapt "androidx.hilt:hilt-compiler:$rootProject.hiltCompilerVersion"

    // KSP
    // implementation "com.google.devtools.ksp:symbol-processing-api:1.6.10-1.0.4"
    
    // Moshi
    implementation "com.squareup.moshi:moshi:$rootProject.moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$rootProject.moshiKotlinVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$rootProject.moshiKotlinCodegenVersion"
    // ksp "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"

    // Retrofit with Moshi Converter
    implementation "com.squareup.retrofit2:converter-moshi:$rootProject.retrofitMoshiConverterVersion"

    // Coil
    implementation "io.coil-kt:coil:$rootProject.coilVersion"

    // Gson
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // Room components
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    annotationProcessor "androidx.room:room-compiler:$rootProject.roomVersion"
    kapt "androidx.room:room-compiler:$rootProject.roomVersion"
    androidTestImplementation "androidx.room:room-testing:$rootProject.roomVersion"

    // Firebase BoM
    implementation platform("com.google.firebase:firebase-bom:$rootProject.firebaseBomVersion")

    // Firebase analytics
    implementation "com.google.firebase:firebase-analytics-ktx"

    // Testing
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.androidXJUnitExtensionVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"

    // Jetpack compose UI tests
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$rootProject.jetpackComposeVersion"
}
